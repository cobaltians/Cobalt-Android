buildscript {
    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.32"
    }
}

repositories {
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    jcenter()
}

project.ext {
    buildToolsVersion="30.0.3"
    minSdkVersion=19 //Android 4.4.0
    targetSdkVersion=30
    compileSdkVersion=30
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode 6
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    api "androidx.appcompat:appcompat:1.2.0"
    api "androidx.exifinterface:exifinterface:1.3.2"
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    api 'com.google.android.material:material:1.3.0'
    api 'junit:junit:4.13.2'
    implementation 'commons-lang:commons-lang:2.6'
}

signing {
    sign configurations.archives
}

group = "org.cobaltians"
archivesBaseName = "cobalt"
version = "1.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'org.cobaltians:cobalt'
                packaging 'aar'
                // optionally artifactId can be defined here
                artifactId 'cobalt'
                description 'An Open Source hybrid framework for iOS and Android allowing teams with varied skills to build hybrid apps smartly.'
                url 'https://cobaltians.org/'

                scm {
                    connection 'scm:git:git://github.com/cobaltians/Cobalt-Android.git'
                    developerConnection 'scm:git:ssh://github.com:cobaltians/Cobalt-Android.git'
                    url 'https://github.com/cobaltians/Cobalt-Android/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name 'Guillaume Gendre'
                        email 'ggendre.web@gmail.com'
                        organizationUrl 'https://cobaltians.org/'
                    }
                    developer {
                        name 'SÃ©bastien Vitard'
                        email 'pro@sebastienvitard.com'
                        organizationUrl 'https://cobaltians.org/'
                    }
                }
            }
        }
    }
}